GitHub Webhook Logger

This project sets up an AWS infrastructure to log details of GitHub webhook events using an API Gateway, Lambda function, S3 bucket, and CloudWatch for monitoring. The Lambda function is triggered by GitHub webhooks, processes the event data, and stores the logs in an S3 bucket.

Project Overview:

* API Gateway: Handles HTTP requests from GitHub webhooks.
* Lambda Function: Processes the incoming webhook data, identifies the changed files in a pull request, and logs this data.
* Lambda Layer: Includes dependencies for the Lambda function.
* S3 Bucket: Stores the logs generated by the Lambda function.
* CloudWatch: Monitors and logs activities for both the API Gateway and Lambda function.

Terraform Resources

AWS Provider
```hcl
provider "aws" {
  region = "us-west-2"
}
```
IAM Role for Lambda
```hcl
resource "aws_iam_role" "lambda_execution_role" {
  name = "lambda_execution_role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [{
      Action = "sts:AssumeRole"
      Effect = "Allow"
      Principal = {
        Service = "lambda.amazonaws.com"
      }
    }]
  })

  managed_policy_arns = [
    "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
  ]
}
```
Lambda Layer

```hcl
resource "null_resource" "install_layer_dependencies" {
  provisioner "local-exec" {
    command = "pip install -r layer/requirements.txt -t layer/python/lib/python3.9/site-packages"
  }
  triggers = {
    trigger = timestamp()
  }
}

data "archive_file" "layer_zip" {
  type        = "zip"
  source_dir  = "layer"
  output_path = "layer.zip"
  depends_on = [
    null_resource.install_layer_dependencies
  ]
}

resource "aws_lambda_layer_version" "lambda_layer" {
  filename          = "layer.zip"
  source_code_hash  = data.archive_file.layer_zip.output_base64sha256
  layer_name        = "env-layer"
  compatible_runtimes = ["python3.9"]
}
```
Lambda Function
```hcl
resource "aws_lambda_function" "github_logger" {
  function_name    = "github_logger"
  role             = aws_iam_role.lambda_execution_role.arn
  handler          = "lambda_function.lambda_handler"
  runtime          = "python3.9"
  filename         = "lambda_function_payload.zip"
  source_code_hash = filebase64sha256("lambda_function_payload.zip")

  layers = [
    aws_lambda_layer_version.lambda_layer.arn
  ]
  environment {
    variables = {
      LOG_BUCKET = aws_s3_bucket.github_logs.bucket
    }
  }
}
```
S3 Bucket
```hcl
resource "aws_s3_bucket" "github_logs" {
  bucket = "github-logs-v2"
}
```
API Gateway
```chl
resource "aws_apigatewayv2_api" "github_webhook_api" {
  name          = "github_webhook_api"
  protocol_type = "HTTP"
}
```
API Gateway Integration and Route
```hcl
resource "aws_apigatewayv2_integration" "lambda_integration" {
  api_id             = aws_apigatewayv2_api.github_webhook_api.id
  integration_type   = "AWS_PROXY"
  integration_uri    = aws_lambda_function.github_logger.invoke_arn
  integration_method = "POST"
}

resource "aws_apigatewayv2_route" "github_webhook_route" {
  api_id    = aws_apigatewayv2_api.github_webhook_api.id
  route_key = "POST /webhook"
  target    = "integrations/${aws_apigatewayv2_integration.lambda_integration.id}"
}

resource "aws_apigatewayv2_stage" "default_stage" {
  api_id      = aws_apigatewayv2_api.github_webhook_api.id
  name        = "$default"
  auto_deploy = true
}
```
Lambda Permissions
```hcl
resource "aws_lambda_permission" "allow_apigateway" {
  statement_id  = "AllowAPIGatewayInvoke"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.github_logger.function_name
  principal     = "apigateway.amazonaws.com"
  source_arn    = "${aws_apigatewayv2_api.github_webhook_api.execution_arn}/*/*"
}
```
CloudWatch Log Groups
```hcl
resource "aws_cloudwatch_log_group" "api_gateway_log_group" {
  name = "/aws/apigateway/github_webhook_api"
}

resource "aws_cloudwatch_log_group" "lambda_log_group" {
  name = "/aws/lambda/github_logger"
}
```
IAM Policy for Logging
```hcl
resource "aws_iam_role_policy" "lambda_logging_policy" {
  role = aws_iam_role.lambda_execution_role.id

  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Action = [
          "logs:CreateLogGroup",
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ],
        Effect   = "Allow",
        Resource = "*"
      },
      {
        Action = [
          "s3:PutObject",
          "s3:GetObject",
          "s3:ListBucket"
        ],
        Effect   = "Allow",
        Resource = [
          aws_s3_bucket.github_logs.arn,
          "${aws_s3_bucket.github_logs.arn}/*"
        ]
      }
    ]
  })
}
```
Lambda Function Code
```python
import json
import boto3
import os
import requests

s3_client = boto3.client('s3')

def lambda_handler(event, context):
    body = json.loads(event['body']) if isinstance(event['body'], str) else event['body']
    commits_url = body['pull_request']['commits_url']
    repo_name = body['repository']['name']

    response = requests.get(commits_url)
    commits = response.json()

    changed_files = set()

    for commit in commits:
        commit_url = commit['url']
        commit_response = requests.get(commit_url)
        commit_json = commit_response.json()

        for file in commit_json['files']:
            changed_files.add(file['filename'])

    log_entry = {
        'repository': repo_name,
        'files_changed': list(changed_files)
    }

    print(json.dumps(log_entry))

    s3_client.put_object(
        Bucket=os.environ['LOG_BUCKET'],
        Key=f"{repo_name}_log.json",
        Body=json.dumps(log_entry)
    )

    return {
        'statusCode': 200,
        'body': json.dumps('Log entry created')
    }
```
Usage

1. Clone the repository and navigate to the project directory.
2. Install the required dependencies for the Lambda layer using pip and package them.
3. Deploy the infrastructure using Terraform: terraform init, terraform apply.
4. Configure your GitHub repository to send webhook events to the API Gateway endpoint.
5. Monitor the API Gateway and Lambda function logs in CloudWatch.
6. Logs of the processed GitHub webhook events will be stored in the specified S3 bucket.

License

This project is licensed under the MIT License.